#class {spell queue} {kill}
#class {spell queue} {open}

#var spellLock 0;

#list spellValidPositions create Flying Fighting Mounted Standing;

#alias spellAdd {
    #list spellqueue[%1] find {%2} {sindex};
    #if {$sindex} {
        diaLog spell Not adding "%2" as it is already in list at position $sindex;
    }   {
        diaLog spell Adding spell "%2" to %1 queue;
        #list spellqueue[%1] add {%2};
        casting_block
    };

}

#alias spellRunner {
    #if { @mpoch{} > $spellLock} {
        #if {&spellqueue[priority][]} {
            #if {@spell_can_cast{}} {
                diaLog spell Casting $spellqueue[priority][1];
                spell_cast_actual $spellqueue[priority][1];
                #list spellqueue[priority] delete 1;
                casting_block;
                #break
            };
        };
        #if {&spellqueue[heal][]} {
            #if {@spell_can_cast{}} {
                diaLog spell Casting $spellqueue[heal][1];
                spell_cast_actual $spellqueue[heal][1];
                #list spellqueue[heal] delete 1;
                casting_block;
                #break
            };
        };
        #if {&spellqueue[any][]} {
            #if {@spell_can_cast{}} {
                diaLog spell Casting $spellqueue[any][1];
                spell_cast_actual $spellqueue[any][1];
                #list spellqueue[any] delete 1;
                casting_block;
                #break
            };
        };
        #if {&spellqueue[combat][]} {
            #if {"$MSDP_POSITION" == "Fighting"} {
                #if {@spell_can_cast{}} {
                    diaLog spell Casting $spellqueue[combat][1];
                    spell_cast_actual $spellqueue[combat][1];
                    #list spellqueue[combat] delete 1;
                    casting_block;
                    #break
                };
            } {
                diaLog spell Not in combat, clearing spell queue;
                #list spellqueue[combat] clear;
            }
        };
        #if {&spellqueue[nco][]} {
            #if {"$MSDP_POSITION" != "Fighting"} {
                #if {@spell_can_cast{}} {
                    diaLog spell Casting $spellqueue[nco][1];
                    spell_cast_actual $spellqueue[nco][1];
                    #list spellqueue[nco] delete 1;
                    casting_block;
                    #break
                };
            };
        };
    }
}

#alias spell_cast_actual {
    #list spellParts create %0;
    diaLog cast %0;
    %0;
    #math spellLock { @max{$spellLock;@mpoch{}} + @spellDelay{$spellParts[1]}}
}

#function {spell_can_cast} {
    #var result 1;
    #list spellValidPositions find {$MSDP_POSITION} {check};
    #if {$check == 0} { #var result 0};
    #regex $PLAYER_FLAGS {-Knees-} { #var result 0 };
    #regex $PLAYER_FLAGS {>BASH<}  { #var result 0 };
}


#nop -- ---------------------------------------------------------------
#nop -- Functions
#nop -- ---------------------------------------------------------------

#nop -- Spell delays are in 1000ths of a second;
#func spellDelay {
    #switch {"%1"} {
        #case {"heal"} #var result 2000;
        #case {"convert"} #var result 12000;
        #case {"shapechange"} #var result 4000;
        #case {"fst"} #var result 3000;
        #case {"charge"} #var result 3000;
        #case {"smart_move"} #var result 250;
        #case {"mobAttack"} #var result 8000;
        #default {
            #var result 2000;
            diaLog spell Unknown delay for %1, using $result ms
            };
    }
}


#nop -- ---------------------------------------------------------------
#nop -- Ticks
#nop -- ---------------------------------------------------------------
#tick {spell queue runner} {spellRunner} {0.2};

#class {spell queue} {close}
#nop -- vim: syntax=tt
