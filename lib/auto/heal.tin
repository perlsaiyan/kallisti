#class {autoheal} {kill}
#class {autoheal} {open}

#tick {autotrue} {autotrue} {10};

#tick {autodamage} {autodamage} {4};
#untick autodamage;

event_register {variable MSDP_GROUP} {autoheal} {#delay {autoheal} {autoheal} {.1}};

#alias autoheal {
    #nop %0;
    #if { @pct{$MSDP_HEALTH;$MSDP_HEALTH_MAX} < 30 } {
            spellAdd priority heal;
    } {
        #foreach *MSDP_GROUP[%*] {_tmp} {
            #if {$MSDP_GROUP[$_tmp][health] < 80 } {
                    #if {$MSDP_GROUP[$_tmp][with_you] == 1} {
                        #nop -- heal $MSDP_GROUP[$_tmp][name];
                        diaLog spell $MSDP_GROUP[$_tmp][name] at $MSDP_GROUP[$_tmp][health];
                        spellAdd heal {heal $MSDP_GROUP[$_tmp][name]};
                    }
            }
            #if {$MSDP_GROUP[$_tmp][stamina] < 40 } {
                    #if {$MSDP_MANA > 300} {
                        #if {@refreshBlacklist{$MSDP_GROUP[$_tmp][name]}} {
                            #nop
                        } {
                            #if {$MSDP_GROUP[$_tmp][with_you] == 1} {
                                #nop -- refresh $MSDP_GROUP[$_tmp][name];
                                spellAdd any {refresh $MSDP_GROUP[$_tmp][name]};
                            }
                        }
                    }
            }
        }
    }
}

#alias autodamage {
        #nop -- temporary damage hack;
        #if { "$MSDP_POSITION" == "Fighting" } {
            #if { @pct{$MSDP_MANA;$MSDP_MANA_MAX} > 20} {
                fst
            }
        }
}

#alias autotrue {
        #if {@hasAffect{true seeing} < 1} {
                spellAdd nco {true};
        };
        #if {@hasAffect{vigor} < 1} {
                #nop -- vigor group;
                spellAdd nco {vigor group};
        };
}

#act {^You heal %w's wounds. (%d healed, now at \%%d)$} {
    #foreach *MSDP_GROUP[%*] {_idx} {
        #if {"$MSDP_GROUP[$_idx][name]" == "%1"} {
            #var $MSDP_GROUP[$_idx][health] %3;
            diaLog spell updated health for $_idx to %3;
            #break
        }
    }
}

#act {^Your wounds begin to close. (%d healed)$} {
    diaLog spell selfheal for %1;
    #foreach *MSDP_GROUP[%*] {_idx} {
        #if {"$MSDP_GROUP[$_idx][name]" == "$name"} {
            diaLog spell old health at $MSDP_GROUP[$_idx][health];
            #math _result {(($MSDP_HEALTH) * 100) / $MSDP_HEALTH_MAX};
            #var $MSDP_GROUP[$_idx][health] $_result;
            diaLog spell updated health for $_idx to $_result;
            #break
        }
    }
}

#act {^A feeling of energy fills you, fighting off the weariness you felt only a moment ago. (%d refreshed)$} {
    diaLog spell selfrefresh for %1;
    #foreach *MSDP_GROUP[%*] {_idx} {
        #if {"$MSDP_GROUP[$_idx][name]" == "$name"} {
            diaLog spell old stam at $MSDP_GROUP[$_idx][stamina];
            #math _result {(($MSDP_STAMINA) * 100) / $MSDP_STAMINA_MAX};
            #var $MSDP_GROUP[$_idx][stamina] $_result;
            diaLog spell updated stam for $_idx to $_result;
            #break
        }
    }
}


#act {^You tap into the vital energy around you and use it to refresh %w's weary body. (%d refreshed, now at \%%d)$} {
    #foreach *MSDP_GROUP[%*] {_idx} {
        #if {"$MSDP_GROUP[$_idx][name]" == "%1"} {
            diaLog spell old stam at $MSDP_GROUP[$_idx][stamina];
            #var $MSDP_GROUP[$_idx][stamina] %3;
            diaLog spell updated stam for $_idx to %3;
            #break
        }
    }
}


#function refreshBlacklist {#list {refreshBlack} {find} {%1} {result}}


#class {autoheal} {close}
#nop -- vim: syntax=tt
