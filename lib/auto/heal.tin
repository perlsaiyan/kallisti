#class {autoheal} {kill}
#class {autoheal} {open}

#tick {autotrue} {autotrue} {10};

#tick {autodamage} {autodamage} {4};
#untick autodamage;

event_register {variable MSDP_GROUP} {autoheal} {autoheal};

#alias autoheal {
    #nop %0;
    #if { @pct{$MSDP_HEALTH;$MSDP_HEALTH_MAX} < 30 } {
            spellAdd priority heal;
    } {
        #foreach *MSDP_GROUP[%*] {_tmp} {
            #if {$MSDP_GROUP[$_tmp][health] < 80 } {
                    #if {$MSDP_GROUP[$_tmp][with_you] == 1} {
                        #nop -- heal $MSDP_GROUP[$_tmp][name];
                        spellAdd heal {heal $MSDP_GROUP[$_tmp][name]};
                    }
            }
            #if {$MSDP_GROUP[$_tmp][stamina] < 40 } {
                    #if {$MSDP_MANA > 300} {
                        #if {@refreshBlacklist{$MSDP_GROUP[$_tmp][name]}} {
                            #nop
                        } {
                            #if {$MSDP_GROUP[$_tmp][with_you] == 1} {
                                #nop -- refresh $MSDP_GROUP[$_tmp][name];
                                spellAdd any {refresh $MSDP_GROUP[$_tmp][name]};
                            }
                        }
                    }
            }
        }
    }
}

#alias autodamage {
        #nop -- temporary damage hack;
        #if { "$MSDP_POSITION" == "Fighting" } {
            #if { @pct{$MSDP_MANA;$MSDP_MANA_MAX} > 20} {
                fst
            }
        }
}

#alias autotrue {
        #if {@hasAffect{true seeing} < 1} {
                spellAdd nco {true};
        };
        #if {@hasAffect{vigor} < 1} {
                #nop -- vigor group;
                spellAdd nco {vigor group};
        };
}

#act {^You heal %w's wounds. (%d healed, now at \%%d)$} {
    #foreach *MSDP_GROUP[%*] {_idx} {
        #if {"$MSDP_GROUP[$_idx][name]" == "%1"} {
            #var $MSDP_GROUP[$_idx][health] %3;
            #break
        }
    }
}

#act {^You tap into the vital energy around you and use it to refresh %w's weary body. (%d refreshed, now at \%%d)$} {
    #foreach *MSDP_GROUP[%*] {_idx} {
        #if {"$MSDP_GROUP[$_idx][name]" == "%1"} {
            #var $MSDP_GROUP[$_idx][stamina] %3;
            #break
        }
    }
}

#func pct {#math result { %1 * 100.0 / %2 }}


#function refreshBlacklist {#list {refreshBlack} {find} {%1} {result}}


#class {autoheal} {close}
#nop -- vim: syntax=tt
