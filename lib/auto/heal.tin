#class {autoheal} {kill}
#class {autoheal} {open}

#tick {autodamage} {autodamage} {4};
#untick autodamage;
#var refreshManaThreshold 300
#var healpct 80
#var refreshpct 50
event_register {variable MSDP_GROUP} {autoheal} {#delay {autoheal} {autoheal} {.1}};

#list healBlack create {Si} {San} {Er} {Yi} {Wu} {raven};

#alias autoheal {
    #nop %0;
    #if { @pct{$MSDP_HEALTH;$MSDP_HEALTH_MAX} < 30 } {
		#switch {"$name"} {
			#case {"Artax"} {spellAdd priority heal};
			#default {spellAdd priority heal}
		};
    } {
        #foreach *MSDP_GROUP[%*] {_tmp} {
#nop            #if {"$MSDP_GROUP[$_tmp][class]" == "MOB"} {#continue};
            #if {@healBlacklist{$MSDP_GROUP[$_tmp][name]} > 0} {#continue};
            #if {$MSDP_GROUP[$_tmp][health] < $healpct } {
                    #if {$MSDP_GROUP[$_tmp][with_you] == 1} {
                        #nop -- heal $MSDP_GROUP[$_tmp][name];
                        diaLog spell $MSDP_GROUP[$_tmp][name] at $MSDP_GROUP[$_tmp][health];
						#switch {"$name"} {
							#case {"Artax"} {
								spellAdd heal {heal $MSDP_GROUP[$_tmp][name]}
								};
							#case {"Sateen"} {
								spellAdd heal {fheal $MSDP_GROUP[$_tmp][name]}
								};
							#default {
								spellAdd heal {heal $MSDP_GROUP[$_tmp][name]};
								};
						}
                    }
            };
            #if {$MSDP_GROUP[$_tmp][stamina] < $refreshpct } {
                    #if {$MSDP_MANA > $refreshManaThreshold} {
                        #if {@refreshBlacklist{$MSDP_GROUP[$_tmp][name]}} {
                            #nop
                        } {
                            #if {$MSDP_GROUP[$_tmp][with_you] == 1} {
                                #nop -- refresh $MSDP_GROUP[$_tmp][name];
                                spellAdd any {refresh $MSDP_GROUP[$_tmp][name]};
                            }
                        }
                    }
            }
        };
		#if {"$mount_heal" == "on"} {
			#if {$MSDP_MOUNT_HEALTH_MAX > 0} {
				#if {@pct{$MSDP_MOUNT_HEALTH;$MSDP_MOUNT_HEALTH_MAX} < 50} {
					spellAdd heal {heal $mount}
				}
			};
		};
    }
}

#alias autodamage {
        #nop -- temporary damage hack;
        #if { "$MSDP_POSITION" == "Fighting" } {
            #if { @pct{$MSDP_MANA;$MSDP_MANA_MAX} > 20} {
                fst
            }
        }
}


#act {^You heal %w's wounds. (%d healed, now at \%%d)$} {
    #foreach *MSDP_GROUP[%*] {_idx} {
        #if {"$MSDP_GROUP[$_idx][name]" == "%1"} {
            #var $MSDP_GROUP[$_idx][health] %3;
            diaLog spell updated health for $_idx to %3;
            #break
        }
    }
}

#act {^Your wounds begin to close. (%d healed)$} {
    diaLog spell selfheal for %1;
    #foreach *MSDP_GROUP[%*] {_idx} {
        #if {"$MSDP_GROUP[$_idx][name]" == "$name"} {
            diaLog spell old health at $MSDP_GROUP[$_idx][health];
            #math _result {(($MSDP_HEALTH) * 100) / $MSDP_HEALTH_MAX};
            #var $MSDP_GROUP[$_idx][health] $_result;
            diaLog spell updated health for $_idx to $_result;
            #break
        }
    }
}

#act {^A feeling of energy fills you, fighting off the weariness you felt only a moment ago. (%d refreshed)$} {
    diaLog spell selfrefresh for %1;
    #foreach *MSDP_GROUP[%*] {_idx} {
        #if {"$MSDP_GROUP[$_idx][name]" == "$name"} {
            diaLog spell old stam at $MSDP_GROUP[$_idx][stamina];
            #math _result {(($MSDP_STAMINA) * 100) / $MSDP_STAMINA_MAX};
            #var $MSDP_GROUP[$_idx][stamina] $_result;
            diaLog spell updated stam for $_idx to $_result;
            #break
        }
    }
}


#act {^You tap into the vital energy around you and use it to refresh %w's weary body. (%d refreshed, now at \%%d)$} {
    #foreach *MSDP_GROUP[%*] {_idx} {
        #if {"$MSDP_GROUP[$_idx][name]" == "%1"} {
            diaLog spell old stam at $MSDP_GROUP[$_idx][stamina];
            #var $MSDP_GROUP[$_idx][stamina] %3;
            diaLog spell updated stam for $_idx to %3;
            #break
        }
    }
}


#function refreshBlacklist {#list {refreshBlack} {find} {%1} {result}}
#function healBlacklist {#list {healBlack} {find} {%1} {result}}

#class {autoheal} {close}
#nop -- vim: syntax=tt
