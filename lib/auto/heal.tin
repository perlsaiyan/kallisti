#class {autoheal} {kill}
#class {autoheal} {open}

#tick {autoheal} {autoheal} {2};
#tick {autotrue} {autotrue} {1};

#tick {autodamage} {autodamage} {4};
#untick autodamage;

#alias autoheal {
    #if { @pct{$MSDP_HEALTH;$MSDP_HEALTH_MAX} < 75 } {
        #if {(@epoch{} - $last_heal) > 1} {
            heal;
            #var last_heal @epoch{};
        }
    } {

        #foreach *MSDP_GROUP[%*] {_tmp} {
            #if {$MSDP_GROUP[$_tmp][health] < 80 } {
                #if {(@epoch{} - $last_heal) > 2} {
                    heal $MSDP_GROUP[$_tmp][name];
                }
            };
            #if {$MSDP_GROUP[$_tmp][stamina] < 40 } {
                    #if {$MSDP_MANA > 300} {
                        #if {@refreshBlacklist{$MSDP_GROUP[$_tmp][name]}} {
                            #nop
                        } {
                            refresh $MSDP_GROUP[$_tmp][name];
                        }
                    }
            };
        };
    }
}

#alias autodamage {
        #nop -- temporary damage hack;
        #if { "$MSDP_POSITION" == "Fighting" } {
            #if { @pct{$MSDP_MANA;$MSDP_MANA_MAX} > 20} {
                fst
            }
        }
}

#alias autotrue {
        #if {@hasAffect{true seeing} < 1} {
            #if {"$MSDP_POSITION" != "Fighting"} {
				#if {(@epoch{} - $last_true) > 10} {
	            	true;
            		#var last_true @epoch{};
	    		};
			};
        };
        #if {@hasAffect{vigor} < 1} {
            #if {"$MSDP_POSITION" != "Fighting"} {
				#if {(@epoch{} - $last_true) > 10} {
	            	vigor group;
            		#var last_true @epoch{};
	    		};
			};
        };
}

#func pct {
 #math result { %1 * 100.0 / %2 }
}


#function refreshBlacklist {#list {refreshBlack} {find} {%1} {result}}


#class {autoheal} {close}
#nop -- vim: syntax=tt
