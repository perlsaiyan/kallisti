#class autobuf kill
#class autobuf open

#tick {auto bufs} {autoBuf} {4}

#var sanctMethod auto
#var lastSanct 0; #var sanctDelay 15;
#var lastGuardian 0; #var guardianDelay 15;
#var lastBlade 0; #var bladeDelay 15;
#var lastDArmor 0; #var darmorDelay 15;
#var lastBark 0; #var barkDelay 15; #var autobark on;
#var lastFFire 0; #var ffireDelay 15;
#var lastVig 0; 
#var lastWarcry 0

#alias autoBuf {

    #if {@hasAffect{haste} < 1} {
        #switch {"$MSDP_CLASS"} {
            #case {"Wizard"} {doHasteSpell};
        }
    };

    #if {@hasAffect{warcry} < 1} {doWarcry};
    #if {@hasAffect{true seeing} < 1} {doTruesee};
    #if {@hasAffect{spiritual guardian} < 1} {doSpiritGuardian};
    #if {@hasAffect{blade barrier} < 1} {doBladeBarrier};
    #if {@hasAffect{faerie fire} < 1} {doFFire};

    #if {@hasAffect{shapechange} < 2} {doShapechange};
    #if {@hasAffect{sanctuary} < 2} {doSanctuary};
    #if {@hasAffect{divine armor} < 2} {doDivineArmor};
    #if {@hasAffect{vigor} < 2} {doVigor};
    #if {@hasAffect{barkskin} < 2} {doBarkskin};

}

#alias doTruesee {
    #switch {"$MSDP_CLASS"} {
        #case {"Prophet"} {spellAdd nco {true}};
        #case {"Druid"} {spellAdd nco {true}};
        #case {"Templar"} {spellAdd nco {true}};
    };
}


#alias doWarcry {
    #if { "$MSDP_CLASS" == "Barbarian" } { spellAdd any {warcry} }
}

#alias doHasteSpell {
    #if {(&MSDP_GROUP) && (&MSDP_GROUP[] > 1) } {
        #if {$MSDP_MANA > 160} {spellAdd nco {haste group}}
    } {
        #if {$MSDP_MANA > 40}  {spellAdd nco {haste}}
    }
}

#alias doDivineArmor {
    #if { ( @epoch{} - $darmorDelay ) < $lastDArmor } { #return } { #var lastDArmor @epoch{}};

    #if { ("$MSDP_CLASS" == "Templar") || ("$MSDP_CLASS" == "Prophet") } {
        #if {(&MSDP_GROUP) && (&MSDP_GROUP[] > 1) } {
            #if {$MSDP_MANA > 160} {spellAdd nco {darmor group}}
        } {
            #if {$MSDP_MANA > 40}  {spellAdd nco {darmor}}
        }
    }
}
#alias doSpiritGuardian {
    #if { ( @epoch{} - $guardianDelay ) < $lastGuardian } { #return };

    #if {"$MSDP_CLASS" == "Templar"} {
        #if {$MSDP_MANA > 40}  {spellAdd nco {spirit}}
        #var lastGuardian @epoch{}
    }
}
#alias doBladeBarrier {
    #if { ( @epoch{} - $bladeDelay ) < $lastBlade } { #return };

    #if {"$MSDP_CLASS" == "Templar"} {
        #if {$MSDP_MANA > 40}  {spellAdd nco {blade}}
        #var lastBlade @epoch{}
    }
}

#alias doVigor {
    #if {@epoch{} < ( $lastVig + 15) } { #return 1 };
    #var lastVig @epoch{};
    #if { "$autovigor" == "on" } {
        #if {(&MSDP_GROUP) && (&MSDP_GROUP[] > 1) } {
            #if {$MSDP_MANA > 100} {spellAdd nco {vigor group}}
        } {
            #if {$MSDP_MANA > 40}  {spellAdd nco {vigor}}
        }
    }
}

#alias doBarkskin {
	#switch {"$MSDP_CLASS"} {
		#case {"Druid"} {
			#if { (@epoch{} - $lastBark < $barkDelay) } { #return 1 } {#var lastBark @epoch{}};
			#if { ("$autobark" == "on") } {
				#if {(&MSDP_GROUP) && (&MSDP_GROUP[] > 1) } {
					#if {$MSDP_MANA > 100} {spellAdd nco {barkskin group}}
				} {
					#if {$MSDP_MANA > 40}  {spellAdd nco {barkskin}}
				}
			}
		};
	}
}

#alias doFFire {
	#switch {"$MSDP_CLASS"} {
		#case {"Druid"} {
			#if { (@epoch{} - $lastFFire < $ffireDelay) } { #return 1 } {#var lastFFire @epoch{}};
				#if {(&MSDP_GROUP) && (&MSDP_GROUP[] > 1) } {
					#if {$MSDP_MANA > 100} {spellAdd nco {ffire group}}
				} {
					#if {$MSDP_MANA > 40}  {spellAdd nco {ffire}}
				}
		};
	}
}

#alias doSanctuary {

    #if { (@epoch{} - $sanctDelay ) > $lastSanct } {
        #switch {"$sanctMethod"} {
            #case {"potion"} {spellAdd nco {sanctPotion}};
            #case {"gsanc"} {spellAdd nco {gt gsanc}};
            #case {"spell"} {doSpellSanct};
            #default {spellAdd nco {doSmartSanct}};
        }
        #var lastSanct @epoch{}
    } {
        diaLog buff Not rerequesting sanct for $sanctDelay seconds.
    }
}

#alias doSpellSanct {
    #if {(&MSDP_GROUP) && (&MSDP_GROUP[] > 1) } {
        #if {$MSDP_MANA > 160} {spellAdd nco {sanct group}}
    } {
        #if {$MSDP_MANA > 40}  {spellAdd nco {sanct}}
    }
}

#alias doSmartSanct {

    #if {@groupIsPresent{Donal} == 1 } {spellAdd nco {gt gsanc}};
    #elseif {@groupIsPresent{Ujarak} == 1 } {spellAdd nco {gt all sanct group}};
    #elseif { "$MSDP_CLASS" == "Prophet" } { doSpellSanct };
    #elseif { "$MSDP_CLASS" == "Templar" } { doSpellSanct };
    #else {sanctPotion};

}

#alias sanctPotion {pg purple;quaff purple};

#alias doShapechange {
    #if {"$pref_shapechange" == "werewolf"} {
        spellAdd nco {werewolf}
    };
    #elseif {&pref_shapechange} {
        spellAdd nco {shapechange $pref_shapechange}
    };
};


#class autobuf close
#nop -- vim: syntax=tt
