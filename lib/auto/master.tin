#class autobuf kill
#class autobuf open

#tick {auto bufs} {autoBuf} {4}

#var sanctMethod auto
#var lastSanct 0; #var sanctDelay 15;
#var lastWarcry 0

#alias autoBuf {

    #if {@hasAffect{haste} < 1} {
        #switch {"$MSDP_CLASS"} {
            #case {"Wizard"} {doHasteSpell};
        }
    };

    #if {@hasAffect{warcry} < 1} {doWarcry};
    #if {@hasAffect{shapechange} < 1} {doShapechange};
    #if {@hasAffect{sanctuary} < 2} {doSanctuary};
    #if {@hasAffect{vigor} < 2} {doVigor};
}

#alias doWarcry {
    #if { "$MSDP_CLASS" == "Barbarian" } { spellAdd any {warcry} }
}

#alias doHasteSpell {
    #if {(&MSDP_GROUP) && (&MSDP_GROUP[] > 1) } {
        #if {$MSDP_MANA > 160} {spellAdd nco {haste group}}
    } {
        #if {$MSDP_MANA > 40}  {spellAdd nco {haste}}
    }
}

#alias doVigor {
    #if {(&MSDP_GROUP) && (&MSDP_GROUP[] > 1) } {
        #if {$MSDP_MANA > 160} {spellAdd nco {vigor group}}
    } {
        #if {$MSDP_MANA > 40}  {spellAdd nco {vigor}}
    }
}

#alias doSanctuary {

    #if { (@epoch{} - $sanctDelay ) > $lastSanct } {
        #switch {"$sanctMethod"} {
            #case {"potion"} {spellAdd nco {sanctPotion}};
            #case {"gsanc"} {spellAdd nco {gt gsanc}};
            #default {spellAdd nco {doSmartSanct}};
        }
        #var lastSanct @epoch{}
    } {
        diaLog buff Not rerequesting sanct for $sanctDelay seconds.
    }
}

#alias doSmartSanct {

    #if {@groupIsPresent{Donal} == 1 } {spellAdd nco {gt gsanc}};
    #else {sanctPotion};

}

#alias sanctPotion {pg purple;quaff purple};

#alias doShapechange {
    #if {"$pref_shapechange" == "werewolf"} {
        spellAdd nco {werewolf}
    } {
        spellAdd nco {shapechange $pref_shapechange}
    };
};


#class autobuf close
#nop -- vim: syntax=tt
