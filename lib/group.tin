#class {group} {kill}
#class {group} {open}

#nop -- ---------------------------------------------------------------
#nop -- Configuration variables
#nop -- ---------------------------------------------------------------
#var groupLoaded 1;
#var GROUP_LEADER {};

#list groupFriends {create} {Chaoran} {Xepher} {Donal} {Dain} {Akima} {Stabitha} {Nagra} {me};

#nop -- ---------------------------------------------------------------
#nop -- Aliases
#nop -- ---------------------------------------------------------------

#alias groupCommand {
    #if {@groupIsFriend{%1}} {
        #if {"%2" == "all recall"} {recall};
        #if {"%2" == "qw"} {recall};
        #if {"%2" == "fire"} {ff group};
        #if {"%2" == "vig"} {vig group};
        #if {"%2" == "stand"} {stand;mount dragonne};
        #if {"%2" == "group all"} {group all};
        #if {"%2" == "sit"} {sit};
        #if {"%2" == "fol self"} {fol self};
        #if {"%2" == "dismount"} {dismount};
        #if {"%2" == "cure"} {cure %1};
        #if {"%2" == "eyes nagra"} {spellAdd nco {true nagra}};
        #if {"%2" == "eyes"} {spellAdd nco {true %1}};
        #if {"%2" == "cv" } {convert};
        #if {"%2" == "cash" } {give $MSDP_GOLD chaoran};
        #if {"%2" == "stop"} {
            #class {zone} kill;
            #class {hk} kill;
            gt yep, done
        };
        #if {"%2" == "heal"} {
            spellAdd heal {heal %1};
        };
        #if {"%2" == "evac"} {
            qp
        };

    } {
        #nop -- Ignore command from non-friend;
    }
}


#nop -- ---------------------------------------------------------------
#nop -- Actions
#nop -- ---------------------------------------------------------------

#action {^You now follow %1.} {
    #var GROUP_LEADER %1;
    diaLog group Setting group leader to %1.;
    #read $lib/follower.tin;
}

#action {^You stop following %1.} {
    #var GROUP_LEADER {};
    #class follower kill;
    diaLog group Unloading follower module.;
}


#action {^{Chaoran|Stabitha} beckons you to follow.} {fol %1;#class zone kill;#class hk kill}

#nop -- ---------------------------------------------------------------
#nop -- Events
#nop -- ---------------------------------------------------------------
event_register {commsGroup} {groupCommand} {groupCommand {%2} {%3}}

#nop -- ---------------------------------------------------------------
#nop -- Functions
#nop -- ---------------------------------------------------------------
#function groupIsFriend {#list {groupFriends} {find} {%1} {result}}

#function groupIsGrouped {
    #var result 0;
    #loop {1} {&MSDP_GROUP[]} {index} {
        #if { "$MSDP_GROUP[$index][name]" == "%1" } { #var result 1; }
    }
}

#class {group} {close}


#nop -- vim: syntax=tt
