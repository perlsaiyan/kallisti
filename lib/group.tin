#class {group} {kill}
#class {group} {open}

#nop -- ---------------------------------------------------------------
#nop -- Configuration variables
#nop -- ---------------------------------------------------------------
#var groupLoaded 1;

#list groupFriends {create} {Chaoran} {Xepher} {Donal} {Dain} {Akima} {Stabitha} {Nagra} {Toc} {Ariella} {Thomdril} {Skade} {Neith} {Bastet} {Anatta} {Rake};

#nop -- ---------------------------------------------------------------
#nop -- Aliases
#nop -- ---------------------------------------------------------------

#alias groupCommand {
    #if {@groupIsFriend{%1}} {
        #if {"%2" == "all recall"} {recall};
        #if {"%2" == "all home"} {home};
        #if {"%2" == "all depart"} {depart};
        #if {"%2" == "qw"} {recall};
        #if {"%2" == "fire"} {spellAdd nco {ff group}};
        #if {"%2" == "vig"} {spellAdd nco {vig group}};
        #if {"%2" == "stand"} {stand;mount dragonne};
        #if {"%2" == "sleep"} {sleep};
        #if {"%2" == "group all"} {group all};
        #if {"%2" == "sit"} {sit};
        #if {"%2" == "fol self"} {fol self};
        #if {"%2" == "dismount"} {dismount};
        #if {"%2" == "cure"} {spellAdd any {cure %1}};
        #if {"%2" == "eyes"} {spellAdd nco {true %1}};
        #if {"%2" == "cv" } {spellAdd any {convert}};
        #if {"%2" == "tnl" } {gt @hNum{$MSDP_EXPERIENCE} experience, @hNum{$MSDP_EXPERIENCE_TNL} TNL};
        #if {"%2" == "stop"} {
            #class {zone} kill;
            #class {hk} kill;
            gt yep, done
        };
        #if {"%2" == "heal"} {
            spellAdd heal {heal %1};
        };
        #if {"%2" == "evac"} {
            qp
        };

    } {
        #nop -- Ignore command from non-friend;
    }
}

#alias groupNewFollower {
    #nop %0;
    #if {@groupIsFriend{%1}} {
        #if {(&MSDP_GROUP) && (&MSDP_GROUP[] > 0) } {
            #if {@groupAmILeader{}} {spellAdd {nco} {group %1}} {point @groupLeader{}}
        } {
            spellAdd {nco} {group $name};
            spellAdd {nco} {group %1}
        }
    } {
        #if {! @groupAmILeader{}} {point @groupLeader{}}
    }
}

#nop -- ---------------------------------------------------------------
#nop -- Actions
#nop -- ---------------------------------------------------------------

#action {^You now follow %1.} {
    #var GROUP_LEADER %1;
    diaLog group Setting group leader to %1.;
    #read $lib/follower.tin;
}

#action {^You stop following %1.} {
    #var GROUP_LEADER {};
    #class follower kill;
    diaLog group Unloading follower module.;
}

#action {^%w starts following you.$} {event_raise {new follower} {%1}}

event_register {new follower} {group new follower} {groupNewFollower %2}

#action {^{Donal|Dain|Rake|Skade|Chaoran|Anatta|Bauchelain} beckons you to follow.} {fol %1;#class zone kill;#class hk kill}

#action {^%w (Goliath) tells you, ':{.*}'$} {%2} {5}

#nop -- ---------------------------------------------------------------
#nop -- Events
#nop -- ---------------------------------------------------------------
event_register {commsGroup} {groupCommand} {groupCommand {%2} {%3}}

#nop -- ---------------------------------------------------------------
#nop -- Functions
#nop -- ---------------------------------------------------------------
#function groupIsFriend {#list {groupFriends} {find} {%1} {result}}

#function groupIsGrouped {
    #var result 0;
    #loop {1} {&MSDP_GROUP[]} {index} {
        #if { "$MSDP_GROUP[$index][name]" == "%1" } { #var result 1; }
    }
}

#function groupIsPresent {
    #var result 0;
    #loop {1} {&MSDP_GROUP[]} {index} {
        #if { ("$MSDP_GROUP[$index][name]" == "%1" ) &&
              ($MSDP_GROUP[$index][with_you] == 1 ) } {
                #var result 1;
        }
    }
}

#function groupLeader {
    #var result {};
    #loop {1} {&MSDP_GROUP[]} {index} {
        #if {$MSDP_GROUP[$index][is_leader] == 1} {#return $MSDP_GROUP[$index][name]}
    }
}

#function groupAmILeader {
    #var result 0;
    #loop {1} {&MSDP_GROUP[]} {index} {
        #if { "$MSDP_GROUP[$index][name]" == "$name" } {
            #if {$MSDP_GROUP[$index][is_leader] == 1} { #return 1 }
        }
    }
    #return 0
}

#var GROUP_LEADER @groupLeader{};

#class {group} {close}


#nop -- vim: syntax=tt
