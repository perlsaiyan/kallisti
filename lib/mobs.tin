#class {mobs} {kill}
#class {mobs} {open}

#nop -- ---------------------------------------------------------------
#nop -- Actions
#nop -- ---------------------------------------------------------------

#nop -- Grab Mob lines
#act {~^\e\[1;37m%1.%s \[{[0-9]+}\]} {
	diaLog kill {multi-add %3};
    #loop {1} {%3} {tmp} {
        #list {mobsInRoom} {add} {@cleanName{%1}};
    };
} {4}
#act {~^\e\[0;37m\e\[1;37m%1.%s \[{[0-9]+}\]} {
	diaLog kill {multi-add2 %3};
    #loop {1} {%3} {tmp} {
        #list {mobsInRoom} {add} {@cleanName{%1}};
    };
} {4}

#act {~^\e\[1;37m%1.%s} {
	diaLog kill {single-add};
    #list {mobsInRoom} {add} {@cleanName{%1}};
} {5}
#act {~^\e\[0;37m\e\[1;37m%1.} {
	diaLog kill {single-add2};
    #list {mobsInRoom} {add} {@cleanName{%1}};
} {5}

#nop -- ---------------------------------------------------------------
#nop -- Aliases
#nop -- ---------------------------------------------------------------

#alias mobsAdd {
    #format mh {%H} {%0}
}

#alias mobAttack {
	#if {&mobsInRoom[] == 0} { #return };
	#var _tmp @mobsKWFromLong{$mobsInRoom[1]};
	#if { "$_tmp" == "" } {
		#list {mobsInRoom} {delete} {1};
		mobAttack
	} {
		#line sub {vars;func} {$openingAttack @mobsKWFromLong{$mobsInRoom[1]}};
		#list {mobsInRoom} {delete} {1};
	}
}

#alias mobsClearList { #list {mobsInRoom} {clear} }

#alias updateMobLine {

    #if {&mobsInRoom[1]} {
        #nop -- #showme {Target: @mobsKWFromLong{$mobsInRoom[1]}} {-3};
    };
    #else {
        #nop -- #showme {Target: None} {-3};
    };

    #nop
}

#tab mobShow
#alias {mobShow} {
  #if {&mobsInRoom[] > 0} {
      #echo {Mobs in the Room};
      #echo {----------------------------------------------------------};
      #loop {1} {&mobsInRoom[]} {_tmp} {
        #var _kw {@mobsKWFromLong{$mobsInRoom[$_tmp]}};
        #format {mobLine} {[%+2s] %.42s (%s) (%d)}
            {$_tmp} {$mobsInRoom[$_tmp]} {$_kw}
            {$mobPR[$mobsInRoom[$_tmp]]};
        #echo {$mobLine}
      }
  } { #echo {There are no mobs in the room.};#cr}
}

#alias mk {#var mobsKWHash[@hash{$mobsInRoom[%1]}] %2;}

#nop -- ---------------------------------------------------------------
#nop -- Events
#nop -- ---------------------------------------------------------------
event_register {map enter room} {mobsClearList} {mobsClearList}
event_register {statusUpdate} {updateMobLine} {updateMobLine}

#nop -- ---------------------------------------------------------------
#nop -- Macros
#nop -- ---------------------------------------------------------------

#macro {OQ} {mobAttack}

#nop -- ---------------------------------------------------------------
#nop -- Functions
#nop -- ---------------------------------------------------------------

#func mobsKWFromLong {
    #format _mktmp {%H} {%0};
    #if {&mobsKWHash[$_mktmp]} {
		#var result $mobsKWHash[$_mktmp];
	} {
		#var result {}
	}
}

#func hash { #format {result} {%H} {%0}; }
#func cleanName {#var result %1; #replace {result} {;} {}};

#nop -- ---------------------------------------------------------------
#nop -- Ticks
#nop -- ---------------------------------------------------------------

#class {mobs} {close}

#nop -- vim: syntax=tt
