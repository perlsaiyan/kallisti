#nop name_of_script: stock.tin


#class {stock} {kill}
#class {stock} {open}

#nop -- stockBasis is the average amount of stock value you want per share;
#var stockBasis 50000
#var stockGrabFlag 0
#var stockTrading 1

#list {stocktags} {create} {Weapons;Siege Equipment;Lamp Oil;Mithril;Pygmy Pigs;Lapis Lazuli;Rordack Cheese}

#nop -- ---------------------------------------------------------------
#nop -- Aliases
#nop -- ---------------------------------------------------------------

#alias {updateStock} {
    #var stockCur[%1] %2;
    #var stockPur[%1] %3;
    #var stockCnt[%1] %4;
    #var stockVal[%1] %5;
    #if {$stockTrading == 1 } {stockTrade %1};
}

#tab stockAuto
#alias stockAuto {

    #if {$stockTrading == 1} {
        #var stockTrading 0;
        #echo {Stock trading is off.}
    } {
        #var stockTrading 1;
        #echo {Stock trading is on.}
    }
}

#tab stockSellOff
#alias stockSellOff {
    #foreach {$stocktags[%*]} {stock} {
        #send {sell $stockCnt[$stock] $stock}
    }
}


#alias stockTrade {
    #math result {$stockBasis * 0.96}; #format stockMin {%d} $result;
    #math result {$stockBasis * 1.04}; #format stockMax {%d} $result;

    #nop -- Speculative, panic buy;
    #if {$stockCur[%0] == 5} {
        #math {_tmp} {20000 - $stockCnt[%0]};
        stockTradeOrAdvise {buy} {$_tmp} {%0};
        syslog PANIC BUY %0 ($_tmp shares);
        #showme <FCC> \
    };
    #elseif {$stockCur[%0] < 35} {
        #nop -- syslog Only sell between 10-35.;
        #math _tmp { ($stockVal[%0] - $stockMax) / $stockCur[%0]};
        #if { $_tmp > 0 } {
            #if { $stockCur[%0] > $stockPur[%0] } {
                stockTradeOrAdvise {sell} {$_tmp} {%0};
            };
        };
        #showme <CCB> \
    };
    #elseif {$stockVal[%0] < $stockMin} {
        #math _tmp { ($stockBasis - $stockVal[%0]) / $stockCur[%0]};
        #if { $_tmp > 0 } {
            stockTradeOrAdvise {buy} {$_tmp} {%0};
        };
    };
    #elseif {$stockVal[%0] > $stockMax} {
        #math _tmp { ($stockVal[%0] - $stockBasis) / $stockCur[%0]};
        #if { $_tmp > 0 } {
            stockTradeOrAdvise {sell} {$_tmp} {%0};
        };
    };
}

#alias stockTradeOrAdvise {
    #if {$MSDP_ROOM_VNUM == 3544} {
        #send {%1 %2 %3};
    } {
        #echo {Advice: %1 %2 %3};
    };
}

#nop -- ---------------------------------------------------------------
#nop -- Actions and Triggers
#nop -- ---------------------------------------------------------------

#action {^------------------------------------------------------------------------$} {
    #if { $stockGrabFlag == 0 } {
        #var stockGrabFlag 1;
        #read $lib/stockvalues.tin
    } {
        #var stockGrabFlag 0;
        #class stockvalues kill;
    }
};

#action {^<Market: the MGSE supervisor} {shares} {5};

#nop -- ---------------------------------------------------------------
#nop -- Functions
#nop -- ---------------------------------------------------------------



#class {stock} {close}


#nop -- vim: syntax=tt
