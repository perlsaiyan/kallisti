#nop name_of_script: msdp.tt

#class {msdp} {kill}
#class {msdp} {open}

#format IAC  %a 255
#format DONT %a 254
#format DO   %a 253
#format WONT %a 252
#format WILL %a 251
#format SB   %a 250
#format SE   %a 240

#format MSDP %a  69

#format VAR  %a  01
#format VAL  %a  02

#var REPORTABLE_VARIABLES {{AREA_NAME};{EXPERIENCE};{EXPERIENCE_TNL};{EXPERIENCE_TNL_MAX};{HEALTH};{HEALTH_MAX};{MANA};{MANA_MAX};{STAMINA};{STAMINA_MAX};{EXP_TNL};{EXP_TNL_MAX};{POSITION};{WORLD_TIME};{ROOM_VNUM};{ROOM_NAME};{ROOM_TERRAIN};{MOUNT_NAME};{COMBAT_STANCE};{OPPONENT_NUMBER};{GOLD};{BANK_GOLD};{MOUNT_STAMINA};{AFFECTS}}

#nop Turn telnet debug off once everything works as it's spammy.

#config {debug telnet} {off}

#event {IAC WILL MSDP}
{
        #send {$IAC$DO$MSDP\};
}

#event {IAC SB MSDP}
{
        #nop Save all variables with an MSDP_ prefix.;
        #var {MSDP_%0} {%1}
}

#nop This event is generated at the end of an MSDP sub negotiation.

#event {IAC SB MSDP IAC SE}
{
        #nop
}

#nop Ask the server to report all keys inside REPORTABLE_VARIABLES

#alias {msdp_report}
{
        #var result {$IAC$SB$MSDP${VAR}REPORT};

        #foreach {$REPORTABLE_VARIABLES[]} {tmp}
        {
                #var result {$result${VAL}$tmp}
        };

        #send {$result$IAC$SE\}
}

#alias {msdp_list}
{
        #send {$IAC$SB$MSDP${VAR}LIST${VAL}%0$IAC$SE\}
}

#alias {msdp_send}
{
        #send {$IAC$SB$MSDP${VAR}SEND${VAL}%0$IAC$SE\}
}

#nop #send {$IAC$SB$MSDP${VAR}XTERM_256_COLORS${VAL}1$IAC$SE};

#class {msdp} {close}

#nop -- vim: syntax=tt
