#nop -- comms logging

#class {comms} {kill}
#class {comms} {open}

#var chat[rows] 10;


#alias {update_chat} {
        diaLog comms update_chat for %0;
        #if {&chat[log][] >= $chat[rows]} {
                #list chat[log] del 1
        };
        #list chat[log] ins -1 {%0};

        draw_comms
}


#nop -- ---------------------------------------------------------------
#nop -- Group communications
#nop -- ---------------------------------------------------------------
#action {^You grouptell: {.*}} {
    event_raise {commsGroup} {$name} {%1} {%0};
} {8}
#action {^**%w: '%2'$} {
   event_raise {commsGroup} {%1} {%2} {%0};
} {8}

#action {^<Clan: %w> '{.*}'} {
    event_raise {commsClan} {%1} {%2} {%0};
} {8}
#action {^You cchat, '{.*}'} {
    event_raise {commsClan} {$name} {%1} {%0};
} {9}

#action {^<Gossip: %w> '{.*}'$} {
    event_raise {commsGos} {%1} {%2} {%0};
} {3}
#action {^<Gossip: %w ({.*})> '{.*}'$} {
    event_raise {commsGos} {%1 (%2)} {%3} {%0};
} {4}

#action {^<Gemote> '%1 %2'$} {
    event_raise {commsGos} {%1} {%2} {%0};
}

#action {^<Development: %w> '%2'$} {
    event_raise {commsDev} {%1} {%2} {%0};
} {8}
#action {^<Development: %w ({.*})> '%3'$} {
    event_raise {commsDev} {%1 (%2)} {%3} {%0};
} {7}

#action {^<Advice: %w> '%2'$} {
    event_raise {commsDev} {%1} {%2} {%0};
} {8}
#action {^<Advice: %w (%w)> '%3'$} {
    event_raise {commsDev} {%1 (%2)} {%3} {%0};
} {7}


#action {^%w ({.*}) tells you, '{.*}'$} {
    event_raise {commsTell} {%1 (%2)} {%3} {%0};
} {8}
#action {^You tell {.*}, '{.*}'$} {
    event_raise {commsTell} {me (to %1)} {%2} {%0};
} {8}

#action {^The winds whisper, '{.*}'} {
    event_raise {commsWorld} {%1} {%0};
} {9}

#action {^[%w:(%w)] {.*}} {
	event_raise {commsChat} {%2} {%0};
} {9}
#action {^[%w:] {.*}} {
	event_raise {commsChat} {%2} {%0};
} {9}

#nop -- ---------------------------------------------------------------
#nop -- Events
#nop -- ---------------------------------------------------------------
event_register {commsGroup} {chatwindow} {update_chat <f25d5f9>%4};
event_register {commsClan}  {chatwindow} {update_chat <f2ff925>%4};
event_register {commsGos}   {chatwindow} {update_chat <fce25f9>%4};
event_register {commsTell}  {chatwindow} {update_chat <ff98825>%4};
event_register {commsWorld} {chatwindow} {update_chat <fe5e4e3>%3};
event_register {commsDev}   {chatwindow} {update_chat <fa5b4b3>%4};
event_register {commsChat}  {chatwindow} {update_chat <f8abeb7>%3};

#class {comms} {close}

#nop -- vim: syntax=tt
