#nop -- comms logging

#class {comms} {kill}
#class {comms} {open}

#var chat[rows] 10;


#alias {update_chat} {
        diaLog comms update_chat for %0;
        #if {&chat[log][] >= $chat[rows]} {
                #list chat[log] del 1
        };
        #list chat[log] ins -1 {%0};

        draw_comms
}


#nop -- ---------------------------------------------------------------
#nop -- Group communications
#nop -- ---------------------------------------------------------------
#action {~^\e[1;36mYou grouptell: {.*}} {
    event_raise {commsGroup} {$name} {%1} {%0};
} {8}
#action {~^\e[1;36m**%w: '%2'\e[0m$} {
   diaLog comms %0 by %1;
   event_raise {commsGroup} {%1} {%2} {%0};
} {8}

#action {~^\e\[1;36m<Clan: %w> '{.*}'} {
    diaLog comms %0 by %1;
    event_raise {commsClan} {%1} {%2} {%0};
} {8}
#action {~^\e\[1;36mYou cchat, '{.*}'} {
    diaLog comms %0 by %1;
    event_raise {commsClan} {$name} {%1} {%0};
} {9}

#action {~^\e[1;35m<Gossip: %w> '{.*}'\e[0m$} {
    event_raise {commsGos} {%1} {%2} {%0};
} {8}
#action {~^\e[1;35m<Gossip: %w (%w)> '{.*}'\e[0m$} {
    event_raise {commsGos} {%1 (%2)} {%3} {%0};
} {8}


#action {~^\e[1;36m%w (%w) tells you, '{.*}'\e[0m$} {
    event_raise {commsTell} {%1 (%2)} {%3} {%0};
} {8}
#action {~^\e[1;36mYou tell {.*}, '{.*}'\e[0m$} {
    event_raise {commsTell} {me (to %1)} {%2} {%0};
} {8}

#action {^The winds whisper, '{.*}'} {
    event_raise {commsWorld} {%1} {%0};
} {9}


#nop -- ---------------------------------------------------------------
#nop -- Events
#nop -- ---------------------------------------------------------------
event_register {commsGroup} {chatwindow} {update_chat %4};
event_register {commsClan}  {chatwindow} {update_chat %4};
event_register {commsGos}   {chatwindow} {update_chat %4};
event_register {commsTell}  {chatwindow} {update_chat %4};
event_register {commsWorld} {chatwindow} {update_chat %3};

#class {comms} {close}

#nop -- vim: syntax=tt
