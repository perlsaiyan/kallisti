#class {map} {kill}
#class {map} {open}

#nop -- ---------------------------------------------------------------
#nop -- Variables
#nop -- ---------------------------------------------------------------

#var mapFile {/tmp/ram/kallisti-map}
#var mapEmbed off

#map create 100000;
#map flag nofollow;
#map flag vtmap on;
#map flag static;
#map flag blockgraphics on;
#map read $ttd/map/msdp.map

#map terrain {Forest} {<228>⟑} DENSE WIDE
#map terrain {Field} {<128>·} DENSE WIDE
#map terrain {Desert} {<cca>·} DENSE NARROW
#map terrain {Inside} {<f707880>▒} DENSE WIDE
#map terrain {Deep Water} {<f2232ff>▒} DENSE WIDE
#map terrain {Water} {<f2284ff>▒} DENSE WIDE

#list mapZoneBlacklist create {BLACKLISTS};

#nop -- ---------------------------------------------------------------
#nop -- Aliases
#nop -- ---------------------------------------------------------------


#alias mapRoom {
	#map goto {$MSDP_ROOM_VNUM} {dig};
	#map set roomarea $MSDP_AREA_NAME;
	#map set roomname $MSDP_ROOM_NAME;
	#map set roomterrain @cleanTerrain{$MSDP_ROOM_TERRAIN};
	#map set roomcolor <178>;

	#if {@mapZoneBlacklist{$MSDP_AREA_NAME}} {#return};

	#foreach {north;south;east;west;up;down} {EXIT} {

		#if {&MSDP_ROOM_EXITS[$EXIT]} {
			#map get {roomexits} {EXIT_LIST};
			#if {&EXIT_LIST[$EXIT] == 0}
			{
				#map get {roomvnum} {EXIT_VNUM} {$MSDP_ROOM_EXITS[$EXIT]};
				#if {"$MSDP_ROOM_EXITS[$EXIT]" != "?"} {
					#map dig {@dA{$EXIT}} {$MSDP_ROOM_EXITS[$EXIT]};
				};
				#if {$EXIT_VNUM == 0}
				{
					#map set {roomcolor} {<118>} {$MSDP_ROOM_EXITS[$EXIT]}
				}
			}
		} {
			#nop -- Room exit doesn't exist, delete;
			#map delete $EXIT
		}

	};

    #nop -- %0 #map map 40 40 overwrite $mapFile;

}

#nop -- ---------------------------------------------------------------
#nop -- Actions
#nop -- ---------------------------------------------------------------

#action {~^\e[1;35m} {
    #echo {VNUM: $MSDP_ROOM_VNUM AREA: $MSDP_AREA_NAME}
} {9}

#nop -- ---------------------------------------------------------------
#nop -- Events
#nop -- ---------------------------------------------------------------
#event {SESSION DISCONNECTED} {
    #map write $ttd/map/msdp.map
}


#nop -- ---------------------------------------------------------------
#nop -- Functions
#nop -- ---------------------------------------------------------------
#function dA {
    #if {"%1" == "east"} {#var result e};
    #elseif {"%1" == "west"} {#var result w};
    #elseif {"%1" == "north"} {#var result n};
    #elseif {"%1" == "south"} {#var result s};
    #elseif {"%1" == "up"} {#var result u};
    #elseif {"%1" == "down"} {#var result d};
    #else {#var result %1};
}

#function mapZoneBlacklist {
	#list mapZoneBlacklist find {%0} {result}
}

#function cleanTerrain {
    #nop -- #format {_tmp} {%p} {%1};
	#line sub var #line strip #var result %1;
}

#class {map} {close}

#nop -- vim: syntax=tt
