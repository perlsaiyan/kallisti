#class {zone} {kill}
#class {zone} {open}



#tick {safety} {#if {"$MSDP_AREA_NAME" != "Baramon Ingrol"} {#class hk kill;#class zone kill}} {.1};

#action {You lost concentration casting 'flamestrike'} {fst $target}


event_register {ehk complete} {baramon next} {baramonNextHrum %2}

#alias baramonNextSoloEng {
    #switch {"%1"} {
        #case "protector" {baramonAttack diamond charge};
        #case "diamond" {baramonAttack ice charge};
        #case "ice" {baramonAttack boulder charge};
        #case "boulder" {baramonAttack hermit charge};
        #case "hermit" {
            #var target protector; #var nextTarget protector;
            #tick {repop check} {hunt protector} {10};
            #showme <cca>Chill and wait for repop I guess;
        };
        #case "fire" {baramonAttack dark charge};
        #case "dark" {
            #map run 36202;
            #var target protector; #var nextTarget protector;
            #tick {repop check} {hunt protector} {10};
            #showme <cca>Chill and wait for repop I guess;
        };
        #case "squirrel" { baramonAttack bear charge};
        #default {syslog Unknown entry for next %1};
    };
}

#alias baramonNextHrum {
    #switch {"%1"} {
        #case "protector" {baramonAttack diamond charge};
        #case "diamond" {baramonAttack ice charge};
        #case "ice" {baramonAttack barb charge};
        #case "barb" {baramonAttack melk charge};
        #case "melk" {baramonAttack boulder};
        #case "boulder" {baramonAttack herm charge};
        #case "fire" {baramonAttack herm};
        #case "dark" {baramonAttack dark};
        #case "herm" {
            #map run 36202;
            #var target protector; #var nextTarget protector;
            #tick {repop check} {hunt protector} {10};
            gt Chill and wait for repop I guess;
        };
        #default {syslog Unknown entry for next %1};
    };
}

#alias baramonNext {
    #switch {"%1"} {
        #case "protector" {baramonAttack diamond fst};
        #case "diamond" {baramonAttack ice fst};
        #case "ice" {baramonAttack barb};
        #case "barb" {baramonAttack melk};
        #case "melk" {baramonAttack boulder};
        #case "guard" {baramonAttack boulder};
        #case "boulder" {ffire group;baramonAttack fire fst};
        #case "fire" {baramonAttack herm};
        #case "herm" {baramonAttack dark};
        #case "dark" {
            #map run 36202;
            #var target protector; #var nextTarget protector;
            #tick {repop check} {hunt protector} {10};
            gt Chill and wait for repop I guess;
        };
        #default {syslog Unknown entry for next %1};
    };
}

#alias safequake {
    #if {$MSDP_PC_IN_ZONE == 0} { quake } { fst %1};
};

#alias {baramonAttack} {
    #read $lib/msdphk.tin;
    #if {"%2" == ""} {
        #var openingAttack kill
    } {
        #var openingAttack %2
    };
    hk %1
}

#act {^You start searching for tracks but then realize} {
    #untick {repop check};
    run_baramon
} {9}

#act {^[* You see your target's tracks} {
    #untick {repop check};
    run_baramon
} {9}


#act {^%w starts following you.} { group all }
#act {^You're too busy fighting to be messing with} {#delay {grouper} {group all} {1}}

#alias specattack {#if {$MSDP_MANA  > 1000} {fst %1};}

#showme <ccb>Overloading Baramon zone accelerators;
#echo {Type run_baramon to start.}

#tab run_baramon;
#alias run_baramon { baramonAttack protector }

#tab run_baramon_hard;
#alias run_baramon_hard {#untick {repop check};baramonAttack fire charge}


#class {zone} {close}
#nop -- vim: syntax=tt

#event {CLASS DESTROYED zone} {#untick {repop check}}
