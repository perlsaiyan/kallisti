#nop -- ---------------------------------------------------------------------
#nop -- Default variables
#nop -- ---------------------------------------------------------------------

#var barchar {-}
#var ttd /home/tom/git/kallisti
#var lib $ttd/lib
#var accountName bhang
#var herpgoal 9000

#script {password} {/usr/bin/pass $accountName@kallistimud.com};

#session {k} {kallistimud.com} {4000}

#message {ACTION} {off}
#message {TAB} {off}
#message {ALIAS} {off}
#message {VARIABLE} {off}

#CONFIG {COLOR MODE} {true}

#nop -- Tins we want at startup
#read $lib/msdp.tin
#read $lib/diag.tin

diag login off

#nop -- LOGIN Things
#class {login} {kill}
#class {login} {open}

#action {^Please enter your account password} {$password[1]; #unvar password;#cr}
#action {^Enter your account name.} {bhang}
#action {*** PRESS ENTER ***} {
    #split 4 4;
    #system {tput clear};
    #cr
}

#action {Active: %w [ Level %d %S %w } {
    diaLog login Player %1, class %4;
    #format curPlayer {%l} {%1};
    #format curClass {%l} {%4};
}

#action {^Welcome to the legendary lands of Kallisti.$} {
    #read $ttd/players/$curPlayer.tin;
    #read $lib/common.tin;
    load_secondary;
    #class {login} {kill};
}


#action {^Reconnecting.$} {
    #line oneshot #tick {launch char} {
        #format {_tmp} {%l} {$MSDP_CHARACTER_NAME};
        #read $ttd/players/${_tmp}.tin;
        #read $lib/common.tin;
        load_secondary;
        #class {login} {kill};
    } {5};
    #echo {Please wait about 5 seconds to load stuff from MSDP.}
}

#al load_secondary {#nop};

#class {login} {close}
#nop -- vim: syntax=tt
